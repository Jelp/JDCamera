apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: "com.jfrog.bintray"
Properties properties = new Properties()
if (project.rootProject.file('local.properties').exists()) {
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
}

android {
    compileSdkVersion 27
    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 4
        versionName "1.0"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:27.1.0'
    compile 'com.theartofdev.edmodo:android-image-cropper:2.6.+'
    compile 'com.android.support:recyclerview-v7:27.1.0'
    compile 'com.github.bumptech.glide:glide:3.7.0'
}

group = 'io.jelp' // Change this to match your package name
version = '1.0' // Change this to match your version number

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

install {
    repositories.mavenInstaller {
        pom.project {
            name 'jdcamera'
            description 'JD Camera Android'
            url 'https://github.com/Jelp/JDCamera'
            groupId 'io.jelp'
            artifactId 'jdcamera'
            version '1.0'

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                connection 'https://github.com/Jelp/JDCamera.git'
                url 'https://github.com/Jelp/JDCamera'

            }
            developers {
                developer {
                    name 'Angel Espinoza'
                }
            }
        }
    }
}

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    pkg {
        repo = "maven"
        // it is the name that appears in bintray when logged
        name = "jdcamera" // TODO
        userOrg = 'jelp-app'
        licenses = ["Apache-2.0"]
        publish true
        version {
            name = '1.0'
            desc = 'JD Camera Android'
            vcsTag = '1.0'
            attributes = ['gradle-plugin': 'com.use.less:com.use.less.gradle:gradle-useless-plugin']
        }
    }
    configurations = ['archives']
}